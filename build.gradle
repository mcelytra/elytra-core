plugins {
    id 'org.gradle.java.experimental-jigsaw' version '0.1.1'
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group = 'org.mcelytra'
version = '1.0.0-SNAPSHOT'

sourceCompatibility = 10

ext.moduleName = 'org.mcelytra.core'

javaModule.name = 'org.mcelytra.core'
compileJava.options.encoding = 'UTF-8'

repositories {
    maven { url = 'https://hub.spigotmc.org/nexus/content/groups/public/' }
    maven { url = 'https://aperlambda.github.io/maven' }
    maven { url = 'https://mcelytra.github.io/maven/' }
    maven { url = 'https://libraries.minecraft.net/' }
    mavenCentral()
}

dependencies {
    compile 'org.mcelytra:elytra-chat:1.0.0-SNAPSHOT'
    compile 'io.netty:netty-all:4.1.34.Final'
    compile 'org.apache.logging.log4j:log4j-core:2.11.1'
    compile 'com.mojang:authlib:1.5.25'
}

javadoc {
    inputs.property("moduleName", moduleName)
    doFirst {
        exclude "**/module-info.java"
        options.addStringOption('XDignore.symbol.file', '-html5')
        options.addStringOption('-module-path', classpath.asPath)
    }
}

jar {
    from 'LICENSE'
    inputs.property("moduleName", moduleName)
    manifest {
        attributes('Automatic-Module-Name': moduleName)
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

signing {
    sign configurations.archives
}

publishing {
    publications {
        elytracore(MavenPublication) {
            from components.java
            groupId = project.group
            artifactId = 'elytra-core'
            version = project.version

            artifact sourcesJar
            artifact javadocJar
        }
    }
    repositories {
        maven {
            url "$buildDir/repo"
        }
    }
}